*$FROM LibraryEx
$EXTERN ArgList, SaveFile;
*$EXTERN Success, Fails

*$FROM Refal5-Lexer
$EXTERN Scan;

*$FROM R5FW-Parser
$EXTERN Parse;

*$FROM R5FW-Transformer
$EXTERN Transform;

*$FROM R5FW-Plainer
$EXTERN Plain;

*$FROM Tests
$EXTERN RunTests;


$ENTRY Go {
  = <Main <ArgList>>;
}

Title { = 'Convertor full Refal-5 sources to Basis Refal subset'; }

Main {
  (e.ProgName) (e.Source) (e.Destination) e.Skipped,
      <PrintLines
        (<Title>)
        ('Source:      ' e.Source)
        ('Destination: ' e.Destination)
      >
      <Parse <Scan e.Source>>:
    {
      Success e.Functions =
        <SaveFile
          (e.Destination)
          <Plain <Transform e.Functions>>
        >;

      Fails e.Errors =
        <PrintLines
          <FormatErrors e.Errors>
        >;
    };

  (e.ProgName) ('_tests_')
    = <RunTests>;

  (e.ProgName) e.Other =
    <PrintLines
      (<Title>)
      ('Bad command line. Expected command line syntax:')
      ('  refgo ' e.ProgName ' source.ref destination.ref')
    >;
}

PrintLines {
  (e.Line) e.Lines =
    <Prout e.Line>
    <PrintLines e.Lines>;

  /* пусто */ = /* ничего не печатаем */;
}

FormatErrors {
  ((s.Line s.Col e.FileName) e.Message) e.Errors =
    <Prout e.FileName ':' <Symb s.Line> ':' <Symb s.Col> ':' e.Message>
    <FormatErrors e.Errors>;

  /* пусто */ = /* всё */
}
